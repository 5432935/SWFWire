<?xml version="1.0" encoding="utf-8"?>
<mx:UIComponent xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx">
	<fx:Script>
		<![CDATA[
			import com.swfwire.utils.DisplayUtil;
			
			public var loader:Loader;
			
			private function loaderCompleteHandler(ev:Event):void
			{
				dispatchEvent(new Event('loaderComplete'));
				invalidateDisplayList();
				validateDisplayList();
			}

			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				if(loader)
				{
					loader.scaleX = 1;
					loader.scaleY = 1;
					
					var rawBounds:Rectangle = loader.getBounds(this);
					
					var offset:Point = new Point(rawBounds.x - loader.x, rawBounds.y - loader.y);
					
					var scaledBounds:Rectangle = DisplayUtil.scaleToFit(rawBounds.width, rawBounds.height, unscaledWidth, unscaledHeight);
					loader.x = scaledBounds.x;
					loader.y = scaledBounds.y;
					loader.width = scaledBounds.width;
					loader.height = scaledBounds.height;
					
					loader.x += -offset.x * loader.scaleX;
					loader.y += -offset.y * loader.scaleY;
				}
			}
			
			public function loadBytes(bytes:ByteArray, context:LoaderContext):void
			{
				if(!loader)
				{
					loader = new Loader();
					loader.contentLoaderInfo.addEventListener(Event.COMPLETE, loaderCompleteHandler);
					addChild(loader);
				}
				loader.loadBytes(bytes, context);
			}
		]]>
	</fx:Script>
</mx:UIComponent>
